/*
 * Copyright 2018 The BookLab Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.bytedeco.javacpp.Loader

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.bytedeco:javacpp:1.1'
    }
}

plugins {
    id "java"
    id "application"
    id "jacoco"
    id "org.jetbrains.kotlin.jvm" version "1.2.40"
    id "org.jmailen.kotlinter" version "1.11.2"
}

ext {
    kotlin_version = '1.2.40'
    ktor_version = '0.9.2'
    jupiter_version = '5.1.1'
    platform = Loader.getPlatform()
}

/* Project configuration */
group = 'nl.tudelft.booklab'
version = '1.0'
sourceCompatibility = 1.8

mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

repositories {
    jcenter()
    maven { url = "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"

    compile "org.openpnp:opencv:3.2.0-1"
    compile "org.bytedeco:javacpp:1.1"
    compile "org.bytedeco.javacpp-presets:leptonica:1.72-1.1"
    compile "org.bytedeco.javacpp-presets:tesseract:3.04-1.1"
    compile "com.google.cloud:google-cloud-vision:1.28.0"
    compile "com.google.guava:guava:25.0-jre"

    runtime "org.slf4j:slf4j-jdk14:1.7.25"
    runtime "org.bytedeco.javacpp-presets:leptonica:1.72-1.1:$platform"
    runtime "org.bytedeco.javacpp-presets:tesseract:3.04-1.1:$platform"

    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    testCompile "org.junit.jupiter:junit-jupiter-api:$jupiter_version"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$jupiter_version"
    testRuntime "org.junit.platform:junit-platform-launcher:1.1.1"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

/* Configure test setup */
test {
    useJUnitPlatform {}

    testLogging {
        events 'passed', 'skipped', 'failed'
    }

    reports {
        html.enabled = true
    }

    finalizedBy jacocoTestReport
}
